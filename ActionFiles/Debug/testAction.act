ACTIONFILE V4

ENABLED True

INSTALL LongDescription="Test script for action"
INSTALL ShortDescription="Test script"
INSTALL Version=0.1.0.0
INSTALL MinEDVersion=9.1.0.0
INSTALL Location=Actions

EVENT onKeyPress, TestFunctions, "", KeyPress $== Shift+F4
EVENT onStartup, onStartup, "", Condition AlwaysTrue

//*************************************************************
// onStartup
// Events: onStartup
//*************************************************************
PROGRAM onStartup

Print **** Action Test harness installed, hit shift+F4 to run

END PROGRAM

//*************************************************************
// TestFunctions
// Events: onKeyPress?(KeyPress $== Shift+F4)
//*************************************************************
PROGRAM TestFunctions

Print Running Test functions at %datetimenow(longdatetime;culture:en-GB)
Set v1 = V1hello
Set v2 = ""
Set v4 = V4hello
Set v5 $= %(v1)
Set v6 = v1
Set v7 = HelloThere
Set n0 = 0
Set n3 = 3
Set nn3 = -3
Set n7 = 7
Set n8 = 8
Set wl1 = one,two,three
Set wl2 = one;two;three
Set wl3 = "\"one\",\"two\",\"three\""
Set wl4 = "\"one\";\"two\";\"three\""
Set prefix = v
Set rroot_1 = V1Hello;VReplacedHello
Set r_name = rroot

// 30 introduces eval to % funcs ints

errorif "EvalSubstring", %substring("abcdefghijkl",Abs(nn3)+2,2) $!= "fg"

// 30.6 introduces error.e with eval engine

errorif "EvalFmtX", %eval('"wkwkkw"+"wl"',Error,N2) $!= "wkwkkwwl"
errorif.e "EvalFmtE", %eval(10.2+20.4,Error,N2)!=30.6
errorif "EvalFmtC", %eval(10.2+20.4,Error,N2) $!= "30.60"

// version 30 fixes Do while

Set res = ""
do
    Set res += A
    print do..while %(res)
While res $!= "AAAAA"

ErrorIf "do.while expr", res $!= "AAAAA"
ErrorIf.E "do.while expr", res != "AAAAA"

// version 30 expands eval to handle strings 
errorif.e "EvalString", %eval('"one"=="one"')!=1

// version 30 adds using variables directly in expressions
Let xxx = n3+1
ErrorIf "EDD", %(xxx) != 4

Let xxx = Floor(10.3)
ErrorIf "Eval", %(xxx) != 10

Let xxx = %eval("Floor(20.3)")
ErrorIf Eval, %(xxx) != 20

Let xxx = %eval("n3+Floor(20.3)")
ErrorIf "Eval", %(xxx) != 23

If Condition AlwaysTrue
    Print Condition always true
Else If Condition AlwaysFalse
    Print Condition error
Else 
    Print Condition error

// version 30 added if.E forms

If.E 10+n3+0!=0
    Print okay iff
ElseIf.E 10/1 == 0
    ErrorIf "IFF - ELSEIFF", Condition AlwaysTrue
Else 
    ErrorIf "IFF - ELSE", Condition AlwaysTrue

If.E 10+n3==0
    ErrorIf "IFF - IFF2", Condition AlwaysTrue
ElseIf.E 10/1 == 10
    Print okay iff elseiff
Else 
    ErrorIf "IFF - ELSE2", Condition AlwaysTrue

// 30 allowed loop count to be an expression

Set res = ""
Loop n3+1
    Set res += A
ErrorIf "Loop expr", res $!= "AAAA"

print while

Set res = ""
While res $!= "AAAA"
    Set res += A
ErrorIf "while expr", res $!= "AAAA"
ErrorIf.E "while expr", res != "AAAA"

if Condition AlwaysTrue
    Set res = ""
    Set res2 = ""
    do
        Set res += A
        rem print do..while %(res)
    While res $!= "AAAAAA"
    while res2 $!= "XXXX"
        Set res2 += X
        rem print while %(res2)
    errorif.e "Do..while", res != "AAAAAA"
    errorif.e "Do..while.e", res2 != "XXXX"
    Print Finish while
else
    print false

if Condition AlwaysFalse
    Set res = ""
    do
        Set res += A
    While res $!= "AAAAA"
    errorif.e "Do..while", res != "AAAAA"
else
    print false

// demo new eval forms for let and while

let i = n3
while.e i > 0
    print loop %(i)
    let  i = i-1
print loop over




// 30 introduced errorif.e
Print check erroriff
ErrorIf.E "Errorif Expr", n3+0!=3

Set array[1] = One
Set array[2] = Two
Set array[3] = Three

Let n10 = (%(n7)*3)+%(n8)
ErrorIf "Eval", %(n10) != 29

Static HNumList = Minus;Trillion;Billion;Million;Thousand;Hundred

ErrorIf "EDD", %systempath(EDDAPPFOLDER) notcontains "EDDiscovery"
ErrorIf "EDD", '%ship("Cobra Mk IV")' $!= "Cobra Mark 4"
ErrorIf "EDD", '%star("Sirius IX-T123b",rroot)' $!= "Sirius , I X T 123 B"

Set datetime = 10/1/2018 12:42
Set datetime2 = 10/1/2024 16:49:20

Set deltatimesec = %datedelta(datetime,datetime2)
Set deltatimesecinv = %datedelta(datetime2,datetime)
ErrorIf "D", %(deltatimesec) != 189403640
ErrorIf "D", %(deltatimesecinv) != -189403640
ErrorIf "D", '%datedeltaformat(deltatimesec, "pos [d day{s|} ][h hour{s|} ][m minute{s|} ][s second{s|}]", "neg [d day{s|} ][h hour{s|} ][m minute{s|} ][s second{s|}]")' $!= "pos 2192 days 4 hours 7 minutes 20 seconds"
ErrorIf "D", '%datedeltaformat(deltatimesecinv, "pos [d day{s|} ][h hour{s|} ][m minute{s|} ][s second{s|}]", "neg [d day{s|} ][h hour{s|} ][m minute{s|} ][s second{s|}]")' $!= "neg 2192 days 4 hours 7 minutes 20 seconds"
ErrorIf "D", '%datedeltadiffformat(datetime,datetime2, "pos [d day{s|} ][h hour{s|} ][m minute{s|} ][s second{s|}]", "neg [d day{s|} ][h hour{s|} ][m minute{s|} ][s second{s|}]")' $!= "pos 2192 days 4 hours 7 minutes 20 seconds"
ErrorIf "D", '%datedeltadiffformat(datetime2,datetime, "pos [d day{s|} ][h hour{s|} ][m minute{s|} ][s second{s|}]", "neg [d day{s|} ][h hour{s|} ][m minute{s|} ][s second{s|}]")' $!= "neg 2192 days 4 hours 7 minutes 20 seconds"

ErrorIf "D", %date(datetime,local;longdate;culture:en-GB) $!= "01 October 2018"
ErrorIf "D", %date(datetime,local;longdatetime;culture:en-GB) $!= "01 October 2018 12:42:00"
ErrorIf "D", %date(datetime,local;datetime;culture:en-GB) $!= "01/10/2018 12:42"
ErrorIf "D", %date(datetime,local;shorttime;culture:en-GB) $!= "12:42"
ErrorIf "D", %date(datetime,local;longtime;culture:en-GB) $!= "12:42:00"
ErrorIf "D", %date(datetime,local;shortdate;culture:en-GB) $!= "01/10/2018"
ErrorIf "D", %date(datetime,local;ticks) $!= "636739945200000000"

ErrorIf "F", %openfile(f1,"c:\\code\\f1.txt",Create) != 1
ErrorIf "F", '%writeline(f1,"line to write")' != 1
ErrorIf "F", %closefile(f1) != 1

ErrorIf "F", %listprocesses(proclist) != 1
ErrorIf "F", %findarray(proclist,"svchost") $== ""
Set pid = "%startprocess(\"notepad.exe\",\"c:\\code\\f1.txt\")"
Sleep 100
ErrorIf "F" , %(pid) == 0
ErrorIf "F", %findprocess("notepad") == 0
ErrorIf "F", %hasprocessexited(pid) $!= "NOTEXITED"
ErrorIf "F", %waitforprocess(pid,500) != 0
ErrorIf "F", %closeprocess(pid) != 1

ErrorIf "F", %openfile(f1,"c:\\code\\f1.txt",Open) != 1
ErrorIf "F", %readline(f1,l1) != 1
ErrorIf "F", l1 $!= "line to write"
ErrorIf "F", %closefile(f1) != 1
ErrorIf "F", %rmdir("c:\\code\\fred") != 0
ErrorIf "F", %mkdir("c:\\code\\fred") != 1
ErrorIf "F", %rmdir("c:\\code\\fred") != 1
ErrorIf "F", %fileexists("c:\\code\\f1.txt") != 1
ErrorIf "F", %fileexists("c:\\code\\f2.txt") != 0
ErrorIf "F", %fileexists("c:\\code\\f2.txt") != 0
ErrorIf "F", %deletefile("c:\\code\\f1.txt") != 1

ErrorIf "F", %findarray(array,"Two") $!= "array[2]"
ErrorIf "F", %findarray(array,"Three","array[2]") $!= "array[3]"
ErrorIf "F", %findarray(array,"Two","array[2]") $!= ""
ErrorIf "F", %findarray(array,"sksks") $!= ""
ErrorIf "F", %expandvars(array,";",1,4,none) $!= "[1] = One;[2] = Two;[3] = Three"
ErrorIf "F", %expandvars(array,";",1,4,nameonly) $!= "[1];[2];[3]"
ErrorIf "F", %expandvars(array,";",1,4,valueonly) $!= "One;Two;Three"
ErrorIf "F", %expandarray(array,";",1,4,none) $!= "One;Two;Three"
ErrorIf "F", %expandarray(array,";",2,1,none) $!= "Two"

ErrorIf "F", %ispresent(v1,v6) $!= "1"
ErrorIf "F", %ispresent(v4,"hel") $!= "1"
ErrorIf "F", %ispresent(v4,"xhel") $!= "0"
ErrorIf "F", %ispresent(v999,"xhel","def") $!= "def"

ErrorIf "F", %ifnotcontains(v1,fred,v4,"false","Blank") $!= "%(v4)"
ErrorIf "F", %ifcontains(v1,V1,v4,"false","Blank") $!= "%(v4)"
ErrorIf "F", %ifcontains(v2,V1,v4,"false","Blank") $!= "Blank"
ErrorIf "F", %ifnotcontains(v1,hel,v4,"false","Blank") $!= "false"

ErrorIf "F", %ifequal(v1,V1hello,v4,"false","Blank") $!= "%(v4)"
ErrorIf "F", %ifequal(v1,v1,v4,"false","Blank") $!= "%(v4)"
ErrorIf "F", %ifequal(v1,v1,"true","false","Blank") $!= "true"
ErrorIf "F", %ifequal(v1,v2,"true","false","Blank") $!= "false"
ErrorIf "F", %ifnotequal(v1,v1,"true","false","Blank") $!= "false"
ErrorIf "F", %ifnotequal(v2,v1,"true","false","Blank") $!= "Blank"

ErrorIf "F", %iffalse(n3,n3,n7) $!= "7"
ErrorIf "F", %ifzero(n0,n3,n7) $!= "3"

ErrorIf "F", %ifge(v2,n7,"true","false","Blank") $!= "Blank"    // check blank n2
ErrorIf "F", %ifge(v2,n7,"true","false") $!= ""

ErrorIf "F", %ifempty(v2,"true","false") $!= "true"
ErrorIf "F", %ifnotempty(v2,"true","false") $!= "false"
ErrorIf "F", %ifempty(v1,"true","false") $!= "false"
ErrorIf "F", %ifnotempty(v1,"true","false") $!= "true"

ErrorIf "F", %ifgt(n7,n0,"true","false") $!= "true"
ErrorIf "F", %ifgt(n0,n7,"true","false") $!= "false"
ErrorIf "F", %iflt(n0,n7,"true","false") $!= "true"
ErrorIf "F", %ifle(n7,n7,"true","false") $!= "true"
ErrorIf "F", %ifge(n7,n7,"true","false") $!= "true"
ErrorIf "F", %ifeq(v2,n0,"true","false") $!= ""
ErrorIf "F", %ifeq("",n0,"true","false","empty") $!= "empty"
ErrorIf "F", %ifeq(v2,n0,"true","false","empty") $!= "empty"
ErrorIf "F", %ifeq(n0,n0,"true","false","empty") $!= "true"
ErrorIf "F", %ifeq(n0,n0,"true","false","empty") $!= "true"
ErrorIf "F", %iftrue(n3,"true","false") $!= "true"
ErrorIf "F", %iftrue(n0,"true","false") $!= "false"
ErrorIf "F", %iffalse(n3,"true","false") $!= "false"
ErrorIf "F", %iffalse(n0,"true","false") $!= "true"
ErrorIf "F", %iffalse(n0,n3,n7) $!= "3"

ErrorIf "F", %ifnonzero(n0,n3,n7) $!= "7"

ErrorIf "F", %alt(v1,"Hello") $!= "V1Hello"
ErrorIf "F", %exist(v1) != 1
ErrorIf "F", %alt(v2,"Hello") $!= "Hello"
ErrorIf "F", %existsdefault(v3,"Hello") $!= "Hello"
ErrorIf "F", %existsdefault(v1,"Hello") $!= "V1hello"
ErrorIf "F", %existsdefault(v1,v4) $!= "V1hello"
ErrorIf "F", %existsdefault(v3,v4) $!= "V4hello"
ErrorIf "expand", %expand(v4) $!= "V4hello"
ErrorIf "expand", %expand("v4") $!= "V4hello"
ErrorIf "indirect", %indirect(v6) $!= "V1hello"
ErrorIf "indirect", %indirect("v6") $!= "V1hello"
ErrorIf "indirect", %indirect("v1") $!= "V1hello"
ErrorIf "F", %i(prefix,6) $!= "v1"
ErrorIf "F", %splitcaps(v7) $!= "Hello There"
ErrorIf "F", %splitcaps("FredJim") $!= "Fred Jim"
ErrorIf "F", %substring("FredJim",1,3) $!= "red"
ErrorIf "F", %substring("Fredrickcat",n3,n7) $!= "drickca"
ErrorIf "F", %indexof("Fredrickcat","rick") != 4
ErrorIf "F", %indexof(v4,"hel") != 2
ErrorIf "F", %lower("HelloThere",",","AndHere") $!= "hellothere,andhere"
ErrorIf "F", %upper("HelloThere",",","AndHere") $!= "HELLOTHERE,ANDHERE"
ErrorIf "F", %join(";",v1,"hello") $!= "V1Hello;hello"
ErrorIf "F", '%trim(" h ell o ")' $!= "h ell o"
ErrorIf "F", %length("hello") != 5
ErrorIf "F", %length(v1) != 7
ErrorIf "WF", %wordcount(wl1,",") != 3
ErrorIf "WF", %wordfind(wl1,"two",",",1,0) != 2
ErrorIf "WF", %wordfind(wl1,"TWO",",",0,0) != 0
ErrorIf "WF", %wordfind(wl1,"tw",",",1,1) != 2
ErrorIf "WF", %wordfind(wl1,"xxx",",",1,1) != 0
ErrorIf "F", %wordof(wl1,2,",") $!= "two"
ErrorIf "F", %wordof(wl2,3) $!= "three"
ErrorIf "F", %wordlistcount(wl3) != 3
ErrorIf "F", %wordlistentry(wl3,1) $!= "two"
ErrorIf "F", %wordlistcount(wl4,";") != 3
ErrorIf "F", %wordlistentry(wl4,1,";") $!= "two"
ErrorIf "F", %replace(v1,"hello",v7) $!= "v1HelloThere"
ErrorIf "F", %replacevar(v1,rroot) $!= "VReplacedHello"
ErrorIf "F", %replacevar(v1,"rroot") $!= "VReplacedHello"
ErrorIf "F", %replacevar(v1,r_name) $!= "VReplacedHello"
ErrorIf "F", %safevarname("\\fred") $!= "__fred"
ErrorIf "F", %hnum(1100000,HNumList) $!= "1.1 Million"

ErrorIf "F", %hnum(1200000000000,HNumList) $!= "1.2 Trillion"
ErrorIf "F",%hnum(1200000000,HNumList)     $!=    "1.2 Billion"
ErrorIf "F",%hnum(1200000,HNumList)        $!=    "1.2 Million"
ErrorIf "F",%hnum(120000,HNumList)         $!=    "120 Thousand"
ErrorIf "F",%hnum(12000,HNumList)          $!=    "12 Thousand"
ErrorIf "F",%hnum(1200,HNumList)           $!=    "1 Thousand 2 Hundred"
ErrorIf "F",%hnum(120,HNumList)            $!=    "120"
ErrorIf "F",%hnum(12,HNumList)             $!=    "12"
ErrorIf "F",%hnum(9.232,HNumList)          $!=    "9.2"
ErrorIf "F",%hnum(0.9232,HNumList)         $!=    "0.9"
ErrorIf "F",%hnum(0.09232,HNumList)        $!=    "0.09"
ErrorIf "F",%hnum(0.009232,HNumList)       $!=    "0.009"
ErrorIf "F",%hnum(0.00009232,HNumList)     $!=    "0.00009"
ErrorIf "F",%hnum(0.0000,HNumList)     $!=    "0"

ErrorIf "F", %abs(-2019.2,N0) $!= "2,019"
ErrorIf "F", %abs(2019.2,N0) $!= "2,019"
ErrorIf "F", %floor(2019.2,N0) $!= "2,019"
ErrorIf "F", %int(2029,D) $!= 2029
ErrorIf "F", %round(2029.2929282,3,"0.#####") $!= 2029.293
ErrorIf "F", %roundnz(0.0000000001,3,"0.###",10) $!= 0.0000000001
ErrorIf "F", %roundscale(0.0000000001,3,"0.###",10,2.0) $!= 0.0000000002
ErrorIf "F", %eval(10+10) != 20
ErrorIf "F", %eval(10+a10,try) $!= Nan

Set v1 = jim 1
Set v2 = "Fred,\"Jim 1\",Sheila  "
If v1 IsOneOf %(v2)
    Print IsOneOf 1 Passed
Else 
    ErrorIf "OneOf", Condition AlwaysTrue

If v1 NotOneOf %(v2)
    ErrorIf "NotOneOf", Condition AlwaysTrue
Else 
    Print NotOneOf 1 Passed

Set v3 = "notjim,\"Jim 1\""

If v3 AnyOfAny %(v2)
    Print Is AnyOfAny Passed
Else 
    ErrorIf "AnyOfAny", Condition AlwaysTrue

If v3 NotAnyOfAny %(v2)
    ErrorIf "NotAnyOfAny", Condition AlwaysTrue
Else 
    Print NotAnyOfAny Passed

Set v4 = jim

If v4 MatchSemicolon "sheila ;george;jim"
    Print MatchSemicolon Passed
Else 
    ErrorIf "MatchSemicolon", Condition AlwaysTrue

If v4 NotMatchSemicolon "sheila ;george;jim"
    ErrorIf "NotMatchSemicolon", Condition AlwaysTrue
Else 
    Print NotMatchSemicolon Passed

Set v5 = Set phasers to stun

If v5 MatchSemicolonList "Set|Calibrate phasers to empty;Set|Calibrate phasers to stun"
    Print MatchSemicolonList Passed
Else 
    ErrorIf "MatchSemicolonList", Condition AlwaysTrue

If v5 NotMatchSemicolonList "Set|Calibrate phasers to empty;Set|Calibrate phasers to stun"
    ErrorIf "NotMatchSemicolonList", Condition AlwaysTrue
Else 
    Print NotMatchSemicolonList Passed

If v5 MatchCommaList "Set|Calibrate phasers to empty,Set|Calibrate phasers to stun"
    Print MatchCommaList Passed
Else 
    ErrorIf "MatchCommaList", Condition AlwaysTrue

If v5 NotMatchCommaList "Set|Calibrate phasers to empty,Set|Calibrate phasers to stun"
    ErrorIf "NotMatchCommaList", Condition AlwaysTrue
Else 
    Print NotMatchCommaList Passed

Print Test Complete %datetimenow(longdatetime;culture:en-GB)

END PROGRAM

